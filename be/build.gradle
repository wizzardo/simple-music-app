import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import java.nio.file.Files
import java.text.SimpleDateFormat

buildscript {
    ext.postgresqlVersion = "42.6.0"
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.9.0"
}

apply plugin: 'application'
apply plugin: 'tools-gradle-build'

group = "com.example"
version = "0.0.1-SNAPSHOT"
java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
        javaParameters = true
    }
}

mainClassName = 'com.example.be.App'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation 'com.wizzardo.tools:tools:0.24-SNAPSHOT'
//    implementation files('../../tools/build/libs/tools-all-0.24-SNAPSHOT.jar')
    implementation('com.wizzardo:http:0.5-SNAPSHOT') {
        exclude group: 'com.wizzardo.tools'
    }

    implementation "org.postgresql:postgresql:$postgresqlVersion"
    implementation 'com.wizzardo.cloud-storage-webdav:base:1.0-SNAPSHOT'
    implementation 'com.wizzardo.cloud-storage-webdav:local:1.0-SNAPSHOT'
    implementation 'com.wizzardo.cloud-storage-webdav:degoo:1.0-SNAPSHOT'
    implementation 'com.wizzardo.cloud-storage-webdav:terabox:1.0-SNAPSHOT'
    implementation 'com.wizzardo.cloud-storage-webdav:s3:1.0-SNAPSHOT'
    implementation 'com.wizzardo.cloud-storage-webdav:webdav:1.0-SNAPSHOT'
//    implementation files('../../cloud-storage-webdav/storages/base/build/libs/base-1.0-SNAPSHOT.jar')
//    implementation files('../../cloud-storage-webdav/storages/local/build/libs/local-1.0-SNAPSHOT.jar')
//    implementation files('../../cloud-storage-webdav/storages/degoo/build/libs/degoo-1.0-SNAPSHOT.jar')
//    implementation files('../../cloud-storage-webdav/storages/terabox/build/libs/terabox-1.0-SNAPSHOT.jar')
//    implementation files('../../cloud-storage-webdav/storages/s3/build/libs/s3-1.0-SNAPSHOT.jar')
//    implementation files('../../cloud-storage-webdav/storages/webdav/build/libs/webdav-1.0-SNAPSHOT.jar')

}


sqlTools {
    migrations {
        enabled = true
        src = 'db/migration'
//        src = 'db/migrations_oracle'
        out = 'migrations.txt'
    }
    tablesGenerator {
        enabled = true
        src = 'src/main/java/com/example/be/db/model'
        out = 'src/main/java/com/example/be/db/generated'
        packageName = 'com.example.be.db.generated'
    }
}

task([type: Jar, description: 'Generates lambda jar with all dependencies'], 'lambdaJar', {
    archiveBaseName = project.name + '-all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { sourceSets.main.runtimeClasspath.collect { !it.isFile() ? it : zipTree(it) } }
    exclude 'META-INF/*'
    with jar

    manifest {
        attributes(
                "Main-Class": 'com.example.be.LambdaApp',
//                "Main-Class": 'com.example.be.TestLambda',
                "buildTime": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "version": "${{ -> version }}"
        )
    }
})

task([description: 'listing all public files'], 'listPublicFiles', {
    doLast {
        def file = new File(projectDir.absolutePath, "src/main/resources/public-list.txt")
        file.parentFile.mkdirs()

        def sout = new StringBuilder(), serr = new StringBuilder()
        def root = new File(projectDir.absolutePath, "src/main/resources/public").toPath()
        Files.walk(root).forEach({
            if (it.toFile().isFile())
                sout.append('/').append(it.subpath(root.getNameCount() - 1, it.getNameCount())).append('\n')
        })

        file.setText(sout.toString())
    }
})
tasks.processResources.dependsOn listPublicFiles


task resolveDependencies {
    setDescription "Resolves all projects dependencies from the repository."
    doLast {
        rootProject.allprojects { project ->
            Set<Configuration> configurations = project.buildscript.configurations + project.configurations
            configurations.findAll { c -> c.canBeResolved }
                    .forEach { c -> c.resolve() }
        }
    }
}